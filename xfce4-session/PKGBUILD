# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

# Maintainer: Rob McCathie <rob@manjaro.org>
## I'm trying to get the light-locker support merged upstream, so hopefully soon we can just use Arch package.

pkgname=xfce4-session
pkgver=4.12.1
pkgrel=3.1
pkgdesc="A session manager for Xfce"
arch=('armv6h' 'armv7h' 'aarch64' 'i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck' 'libsm' 'polkit' 'xorg-iceauth' 'xorg-xinit'
         'xorg-xrdb' 'polkit-gnome' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('gnome-keyring: for keyring support when GNOME compatibility is enabled'
            'xscreensaver: for locking screen with xflock4'
            'gnome-screensaver: for locking screen with xflock4'
            'xlockmore: for locking screen with xflock4'
            'slock: for locking screen with xflock4')
replaces=('xfce-utils')
install=$pkgname.install
source=(http://archive.xfce.org/src/xfce/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2
        xfce-polkit-gnome-authentication-agent-1.desktop
        add-lightdm-support.patch
        0001-Pass-VT-number-to-xinit-if-XDG_VTNR-is-set.patch
        dbus-update-environment.patch)
sha256sums=('97d7f2a2d0af7f3623b68d1f04091e02913b28f9555dab8b0d26c8a1299d08fd'
            '74c94c5f7893d714e04ec7d8b8520c978a5748757a0cdcf5128492f09f31b643'
            'a9f74ed1b4b51b334fcfeb2320670f453e8623c6fceb554393a14920723765e4'
            '7ea702a1d3391d36958cff57496ba2474592dd915cf7569a65501522c2410cf1'
            '0a3f23c2ee1b73cec3ecb999b2cc1b5d97e7023482e42b418e6f14cd4f671c0b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Add lightdm and light-locker support to xflock4 script
  patch -Np1 -i "${srcdir}/add-lightdm-support.patch"

  # https://bugs.archlinux.org/task/44285
  patch -Np1 -i "${srcdir}/0001-Pass-VT-number-to-xinit-if-XDG_VTNR-is-set.patch"

  # Support using existing dbus-daemon
  patch -Np1 -i ../dbus-update-environment.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # Provide a default PolicyKit Authentication Agent (FS#42569)
  install -d "$pkgdir/etc/xdg/autostart"
  cp "$srcdir/xfce-polkit-gnome-authentication-agent-1.desktop" \
    "$pkgdir/etc/xdg/autostart/"
}

# vim:set ts=2 sw=2 et:
