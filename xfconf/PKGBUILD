# Based on the file created for Arch Linux by:
# Maintainer : Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=xfconf
pkgver=4.11.0
pkgrel=1
pkgdesc="A simple client-server configuration storage and query system"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
# http://www.xfce.org/documentation/requirements
# keep dbus, see also http://bugs.archlinux.org/task/14536
depends=('libxfce4util' 'dbus-glib' 'dbus')
makedepends=('perl-extutils-depends' 'perl-extutils-pkgconfig' 'glib-perl'
             'intltool' 'gtk-doc' 'chrpath')
options=('!emptydirs')
source=(http://archive.xfce.org/src/xfce/$pkgname/4.11/$pkgname-$pkgver.tar.bz2)
sha256sums=('8667fc7e90fb760979fb7da75157aead000ae206d24c4ea55b6f2b5631d905d6')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --enable-gtk-doc \
    --with-perl-options=INSTALLDIRS="vendor" \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # fix insecure rpath, http://bugs.archlinux.org/task/19980
  chrpath -d "$pkgdir/usr/lib/perl5/vendor_perl/auto/Xfce4/Xfconf/Xfconf.so"
}

# vim:set ts=2 sw=2 et:
