# Maintainer: Guillaume Benoit <guillaume@manjaro.org>

pkgbase=pamac
pkgname=('pamac' 'pamac-tray-appindicator')
_pkgver=6.2.4
pkgver=$_pkgver
pkgrel=1
pkgdesc="A Gtk3 frontend for libalpm"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/manjaro/pamac"
license=('GPL3')
depends=('glib2>=2.42' 'json-glib' 'libsoup' 'dbus-glib' 'polkit' 'vte3>=0.38' 'gtk3>=3.22'
         'libnotify' 'desktop-file-utils' 'pacman>=5.0' 'pacman<5.1' 'gnutls>=3.4'
         'appstream-glib' 'archlinux-appstream-data' 'pacman-mirrorlist')
makedepends=('gettext' 'meson' 'vala>=0.36.6' 'libappindicator-gtk3' 'git')
options=(!emptydirs)

source=("pamac-$pkgver-$pkgrel.tar.gz::$url/archive/v$_pkgver.tar.gz"
        #"git-$pkgver-$pkgrel.patch::https://github.com/manjaro/pamac/compare/v$_pkgver...master.patch"
       )
sha256sums=('92e734abd1a3bec26571545edc2a96cf0340f2c3f493a21b508e80310bf4bf74')

prepare() {
  cd "$srcdir/pamac-$pkgver"
  # patches here
  #patch -p1 -i "$srcdir/git-$pkgver-$pkgrel.patch"

  # adjust version string
  sed -i -e "s|\"$_pkgver\"|\"$pkgver-$pkgrel\"|g" src/manager_window.vala
}

build() {
  cd "$srcdir/pamac-$pkgver"
  mkdir -p builddir
  cd builddir
  meson --prefix=/usr --sysconfdir=/etc -Denable-appindicator=true

  # build
  ninja
}

package_pamac() {
  optdepends=('pamac-tray-appindicator: tray icon for KDE')
  backup=('etc/pamac.conf')
  replaces=('pamac-aur')
  install=pamac.install
  cd "$srcdir/pamac-$pkgver"
  cd builddir
  DESTDIR="$pkgdir" ninja install
  # enable systemd timer
  mkdir -p "$pkgdir/etc/systemd/system/multi-user.target.wants"
  ln -sf "/usr/lib/systemd/system/pamac-cleancache.timer" "$pkgdir/etc/systemd/system/multi-user.target.wants"
  ln -sf "/usr/lib/systemd/system/pamac-mirrorlist.timer" "$pkgdir/etc/systemd/system/multi-user.target.wants"
  # remove pamac-tray-appindicator
  rm "$pkgdir/usr/bin/pamac-tray-appindicator"
  rm "$pkgdir/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}

package_pamac-tray-appindicator() {
  pkgdesc="Tray icon using appindicator which feets better in KDE"
  depends=('pamac' 'libappindicator-gtk3')
  cd "$srcdir/pamac-$pkgver"
  install -Dm755 "builddir/src/pamac-tray-appindicator" "$pkgdir/usr/bin/pamac-tray-appindicator"
  install -Dm644 "data/applications/pamac-tray-appindicator.desktop" "$pkgdir/etc/xdg/autostart/pamac-tray-appindicator.desktop"
}
# vim:set ts=2 sw=2 et:
