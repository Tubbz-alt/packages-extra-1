# Based on the file created for Arch Linux by:
# Contributor: twa022 <twa022 at gmail dot com>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=xfce4-pulseaudio-plugin
pkgver=0.2.4
pkgrel=1
pkgdesc="Pulseaudio plugin for Xfce4 panel"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
license=('GPL2')
url="http://goodies.xfce.org/projects/panel-plugins/xfce4-pulseaudio-plugin"
groups=('xfce4-goodies')
depends=('xfce4-panel>=4.11.0' 'libpulse' 'libkeybinder3')
optdepends=('ido: appindicator support'
            'pavucontrol: default pulseaudio mixer')
makedepends=('intltool')
install="${pkgname}.install"
source=(http://archive.xfce.org/src/panel-plugins/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2
        use-panel-icons.patch)
sha256sums=('8d9330ddf1d44a864a36d566cce4b76d4f859c5984bba7653d7dc39aa24d5c3e'
            'bce647fdeea4bc682a5eec0a84f57996690cbc982efbfeb0691e8d9eb4e9405e')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i $srcdir/use-panel-icons.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # clean-up as we don't use these icons
  rm -r $pkgdir/usr/share/icons/hicolor/scalable/status/
}
