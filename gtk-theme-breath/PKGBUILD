# $Id$
# Maintainer: Ramon Buld√≥ <rbuldo@gmail.com>

pkgname=gtk-theme-breath
pkgver=5.9.0
pkgrel=1
_gitcommit=da2706640f457f89de6c26312e391b244ff550b4
pkgdesc="Breath widget theme for GTK 2 and 3"
arch=('any')
url='https://github.com/manjaro/artwork-breath-gtk'
license=(LGPL)
makedepends=(extra-cmake-modules qt5-tools)
source=("breath-$pkgver-$pkgrel.tar.gz::$url/archive/$_gitcommit.tar.gz")
md5sums=('e06852165203459c3f5367650e19db6e')

prepare(){
  mv $srcdir/artwork-breath-gtk-$_gitcommit $srcdir/$pkgname
  mkdir -p build
}

build(){
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DWITH_GTK3_VERSION=3.20
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
 
