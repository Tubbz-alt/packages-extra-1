# Maintainer : Ramon Buldo <ramon@manjaro.org>

pkgbase=manjaro-settings-manager
pkgname=('manjaro-settings-manager' 'manjaro-settings-manager-kcm' 
         'manjaro-settings-manager-notifier' 'manjaro-settings-manager-knotifier')
pkgver=0.5.4
pkgrel=14
_git=0.5.4
pkgdesc="Manjaro Linux System Settings Tool"
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
url="https://github.com/manjaro/manjaro-settings-manager"
license=("GPL")
depends=('icu>=61.1' 'qt5-base>=5.9.1' 'hwinfo' 'kitemmodels' 'kauth' 
         'kcoreaddons' 'ckbcomp' 'xdg-utils')
optdepends=('manjaro-settings-manager-notifier: qt-based'
            'manjaro-settings-manager-knotifier: knotifications-based')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools' 'knotifications' 
             'kconfigwidgets' 'kcmutils')
conflicts=('kcm-msm')
source=("msm-$pkgver-$pkgrel.tar.gz::$url/archive/$_git.tar.gz"
        'icu61.patch::https://github.com/manjaro/manjaro-settings-manager/commit/b49453649ba5e8bf39ed4361807f6be9e1dac3cd.patch'
        '140.patch::https://github.com/manjaro/manjaro-settings-manager/commit/b329c132e1043e0462e0ccb39cc0eecc186d2cfb.patch'
        'pacman51.patch::https://github.com/manjaro/manjaro-settings-manager/commit/d60f1db3f9a2fbadf8c330ae072a4f2b69e3d253.patch')
sha256sums=('d14463432cab5aef99806aa694407836ce6813a4f9c563ad72c66eecda61cebc'
            '34eb623dc4ce43756390f1daca0315a647c10228cc321ab4a51eb347696f5f50'
            'd8da33e431e5652ffc523ec0f4f7d1b56dc4205608b29fc5573cc63714ac98a9'
            '9db374416aaf46866e9ce6148bd24be8c7b5bc9033056e8fc03b0d06204acf1e')

prepare() {
  cd "$srcdir/${pkgbase}-${_git}"
  patch -p1 -i "$srcdir/icu61.patch"
  patch -p1 -i "$srcdir/140.patch"
  patch -p1 -i "$srcdir/pacman51.patch"
}

build() {
  cd "$srcdir/${pkgbase}-${_git}"
  mkdir -p build
  cd build
  cmake ../ \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DSYSCONF_INSTALL_DIR=/etc
  CXXFLAGS+="-std=gnu++98" make
}

package_manjaro-settings-manager() {
  cd "$srcdir/${pkgbase}-${_git}/build"
  make DESTDIR=${pkgdir} install 
  rm -rf $pkgdir/usr/bin/msm_notifier
  rm -rf $pkgdir/usr/bin/msm_kde_notifier
  rm -rf $pkgdir/usr/lib/qt
  rm -rf $pkgdir/usr/share/kservices5
  rm -rf $pkgdir/usr/share/applications/msm_notifier_settings.desktop
  rm -rf $pkgdir/usr/share/applications/msm_kde_notifier_settings.desktop
  rm -rf $pkgdir/etc/xdg
}

package_manjaro-settings-manager-kcm() {
  pkgdesc="Manjaro Linux System Settings Tool (KCM for Plasma 5)"
  depends=('manjaro-settings-manager' 'kcmutils' 'kconfigwidgets')
  replaces=('kcm-msm')
  cd "$srcdir/${pkgbase}-${_git}/build"
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/etc  
  rm -rf $pkgdir/usr/bin
  rm -rf $pkgdir/usr/lib/kauth
  rm -rf $pkgdir/usr/share/{applications,dbus-1,icons,polkit-1}
}

package_manjaro-settings-manager-notifier() {
  pkgdesc="Manjaro Linux System Settings Tool (Notifier)"
  depends=('manjaro-settings-manager')
  provides=('manjaro-settings-manager-kde-notifier')
  conflicts=('manjaro-settings-manager-kde-notifier')
  cd "$srcdir/${pkgbase}-${_git}/build"
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/etc/dbus-1
  rm -rf $pkgdir/etc/xdg/autostart/msm_kde_notifier.desktop
  rm -rf $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/share/{kservices5,dbus-1,icons,polkit-1}
  rm -rf $pkgdir/usr/share/applications/manjaro*
  rm -rf $pkgdir/usr/share/applications/msm_kde_notifier_settings.desktop
  rm -rf $pkgdir/usr/bin/manjaro*
  rm -rf $pkgdir/usr/bin/msm_kde_notifier
}

package_manjaro-settings-manager-knotifier() {
  pkgdesc="Manjaro Linux System Settings Tool (Notifier for Plasma 5)"
  depends=('manjaro-settings-manager' 'knotifications')
  conflicts=('manjaro-settings-manager-notifier')
  replaces=('manjaro-settings-manager-kde-notifier')
  cd "$srcdir/${pkgbase}-${_git}/build"
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/etc/dbus-1
  rm -rf $pkgdir/etc/xdg/autostart/msm_notifier.desktop
  rm -rf $pkgdir/usr/lib/
  rm -rf $pkgdir/usr/share/{kservices5,dbus-1,icons,polkit-1}
  rm -rf $pkgdir/usr/share/applications/manjaro*
  rm -rf $pkgdir/usr/share/applications/msm_notifier_settings.desktop
  rm -rf $pkgdir/usr/bin/manjaro*
  rm -rf $pkgdir/usr/bin/msm_notifier
} 
