# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>
# Maintainer: Roland Singer <roland[at]manjaro[dog]org>

pkgname=manjaro-settings-manager
pkgver=0.4.14
pkgrel=1
pkgdesc="Manjaro Linux System Settings Tool"
arch=('armv6h' 'armv7h' 'i686' 'x86_64')
url="https://github.com/manjaro/manjaro-settings-manager"
license=('GPL')
depends=('ckbcomp' 'qt5-base' 'icu' 'hwinfo' 'kitemmodels' 'libxkbcommon-x11')
makedepends=('qt5-tools')
optdepends=('kdesu4: for KDE 4'
            'kde-cli-tools: for Plasma 5'
            'gksu: for XFCE, Gnome, LXDE, Cinnamon'
            'gnome-keyring: for password management'
            'polkit: as an alternative su tool')
replaces=('manjaro-system-settings')
provides=('manjaro-system-settings')
conflicts=('manjaro-system-settings')
install=$pkgname.install
source=("https://github.com/manjaro/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('4c29c7aa353fa1fbbf5913f86cb00d63e59b2bd0ef5230dc42baad194bc2ac63')

prepare() {
  cd "$srcdir/$pkgname-$pkgver/"
  # patches here
}

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  /usr/lib/qt/bin/lrelease $pkgname/$pkgname.pro
  /usr/lib/qt/bin/lrelease $pkgname-daemon/$pkgname-daemon.pro

  qmake-qt5

  # Build fix
  make sub-global-all
  cp global/libglobal.a libglobal.a

  make all
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make INSTALL_ROOT="${pkgdir}" install
}
