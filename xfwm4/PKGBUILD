# Based on the file created for Arch Linux by:
# Maintainer : Piotr Gorski <sir_lucjan@bridgelinux.pl>

# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Maintainer: Roland Singer <roland@manjaro.org>

pkgname=xfwm4
_pkgname=xfwm4
pkgver=4.12.2
pkgrel=1.3
_git=e158a880b6b97d346b88ef02783def4ef1ddf870
pkgdesc="Xfce window manager"
arch=('armv6h' 'armv7h' 'aarch64' 'i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4')
depends=('libxfce4ui' 'libwnck' 'libdrm' 'libxpresent' 'hicolor-icon-theme' 'exo')
makedepends=('intltool' 'xfce4-dev-tools')
options=('!libtool')
install=$_pkgname.install
source=(#http://archive.xfce.org/src/xfce/$_pkgname/4.12/$_pkgname-$pkgver.tar.bz2
        $pkgname-$pkgver-$pkgrel.tar.bz2::http://git.xfce.org/xfce/$pkgname/snapshot/$pkgname-$_git.tar.bz2)
sha256sums=('e612b6d17482315de1e3e21c1cda8ed44010fd6aeba923e8461b4a7e77901cb6')

provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")

prepare() {
  #cd "$srcdir/$pkgname-$pkgver"
  cd "$srcdir/$pkgname-$_git"
  sed -i -e 's|micro], \[0|micro], \[2|g' configure.ac.in
  sed -i -e 's|@REVISION@|MANJARO|g' configure.ac.in
  sed -i -e 's|git]|]|' configure.ac.in
}

build() {
  #cd "$srcdir/$_pkgname-$pkgver"
  cd "$srcdir/$pkgname-$_git"

  #./configure \
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-startup-notification \
    --enable-randr \
    --enable-compositor \
    --enable-xsync \
    --disable-debug
  make
}

package() {
  #cd "$srcdir/$_pkgname-$pkgver"
  cd "$srcdir/$pkgname-$_git"
  make DESTDIR="$pkgdir" install
}
