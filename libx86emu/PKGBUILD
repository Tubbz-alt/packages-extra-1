# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>

pkgname=libx86emu
pkgver=1.11_1
_fn="$pkgname-${pkgver/_/-}.1.src.rpm"
pkgrel=2
pkgdesc='x86 emulation library'
arch=('x86_64' 'i686' 'armv6h' 'armv7h' 'aarch64')
url='http://download.opensuse.org/source/factory/repo/oss/suse/src/'
license=('BSD')
depends=('glibc')
makedepends=('rpmextract')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/$_fn")
sha256sums=('0d944e4ea5fb6711d6f0ce258c5011bcf9c5b1964dbd09f9a6bbe777890952fe')

prepare() {
  # Wish they would just provide a normal $pkgname-$pkgver.tar.gz...
  rpmextract.sh "$_fn"
  tar Jxf "${_fn%-*.*}.tar.xz"
}

build() {
  make -C "${_fn%-*.*}" LIBDIR=/usr/lib
}

package() {
  make -C "${_fn%-*.*}" LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname-${pkgver%_*}/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
