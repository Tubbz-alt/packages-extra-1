# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Author: horst3180 @ deviantart

pkgname=vertex-themes
pkgver=20161125
pkgrel=2
pkgdesc='Vertex Gtk2, Gtk3, Metacity, Xfwm, Cinnamon and GNOME Shell themes.'
_gnomever=3.22
_commit=c861918a7fccf6d0768d45d790a19a13bb23485e
arch=('any')
url='http://horst3180.deviantart.com/art/Vertex-Theme-470663601'
license=('GPL3')
depends=('gtk-engine-murrine')
source=("vertex-$_commit.tar.gz::https://github.com/horst3180/Vertex-theme/archive/$_commit.tar.gz")
sha256sums=('87c4c356fa10d7252270e6f3a8b0d459eedf5e4f9c9b904e3f5d8999c195638c')

build() {
  cd vertex-theme-${_commit}
  ./autogen.sh --prefix=/usr --with-gnome=${_gnomever}
}
package() {
  cd vertex-theme-${_commit}

  make DESTDIR="$pkgdir" install
  cd extra
  for name in "Chrome" "Firefox" "Vertex-Plank" "Vertex_alt_metacity"; do
    find "$name" -type f -not -name *~ -exec install -Dm644 '{}' "$pkgdir/usr/share/themes/{}" \;
  done

  cd ..
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
  install -Dm644 README.md "$pkgdir/usr/share/themes/Vertex/README"
}
